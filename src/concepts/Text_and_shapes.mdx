---
layout: '@layouts/Docs.astro'
sidebar-title: Text and shapes
page-title: Concepts / Text and shapes
---

# Text and shapes 


Labels are advantageous because:

-   They allow \"series\" values to be converted to text and placed on
    charts. This means they are ideal to display values that cannot be
    known before time, such as price values, support and resistance
    levels, of any other values that your script calculates.
-   Their positioning options are more flexible that those of the
    `plot*()` functions.
-   They offer more display modes.
-   Contrary to `plot*()` functions, label-handling functions can be
    inserted in conditional or loop structures, making it easier to
    control their behavior.
-   You can add tooltips to labels.

One drawback to using labels versus
[plotchar()](https://www.tradingview.com/pine-script-reference/v5/#fun_plotchar)
and
[plotshape()](https://www.tradingview.com/pine-script-reference/v5/#fun_plotshape)
is that you can only draw a limited quantity of them on the chart. The
default is \~50, but you can use the `max_labels_count` parameter in
your
[indicator()](https://www.tradingview.com/pine-script-reference/v5/#fun_indicator)
or
[strategy()](https://www.tradingview.com/pine-script-reference/v5/#fun_strategy)
declaration statement to specify up to 500. Labels, like
`lines and boxes <PageLinesAndBoxes>`, are
managed using a garbage collection mechanism which deletes the oldest
ones on the chart, such that only the most recently drawn labels are
visible.

Your toolbox of built-ins to manage labels are all in the `label`
namespace. They include:

-   [label.new()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
    to create labels.
-   `label.set_*()` functions to modify the properties of an existing
    label.
-   `label.get_*()` functions to read the properties of an existing
    label.
-   [label.delete()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete)
    to delete labels
-   The
    [label.all](https://www.tradingview.com/pine-script-reference/v5/#var_label%7Bdot%7Dall)
    array which always contains the IDs of all the visible labels on the
    chart. The array\'s size will depend on the maximum label count for
    your script and how many of those you have drawn.
    `aray.size(label.all)` will return the array\'s size.

### Creating and modifying labels

The
[label.new()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
function creates a new label. It has the following signature:

``` text
label.new(x, y, text, xloc, yloc, color, style, textcolor, size, textalign, tooltip) → series label
```

The *setter* functions allowing you to change a label\'s properties are:

-   [label.set_x()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_x)
-   [label.set_y()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_y)
-   [label.set_xy()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy)
-   [label.set_text()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_text)
-   [label.set_xloc()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xloc)
-   [label.set_yloc()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_yloc)
-   [label.set_color()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_color)
-   [label.set_style()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_style)
-   [label.set_textcolor()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_textcolor)
-   [label.set_size()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_size)
-   [label.set_textalign()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_set_textalign)
-   [label.set_tooltip()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_tooltip)

They all have a similar signature. The one for
[label.set_color()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_color)
is:

``` text
label.set_color(id, color) → void
```

where:

-   `id` is the ID of the label whose property is to be modified.
-   The next parameter is the property of the label to modify. It
    depends on the setter function used.
    [label.set_xy()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy)
    changes two properties, so it has two such parameters.

This is how you can create labels in their simplest form:

``` pine
//@version=5
indicator("", "", true)
label.new(bar_index, high)
```

![image](@assets/concepts/TextAndShapes-CreatingLabels-01.png)

Note that:

-   The label is created with the parameters `x = bar_index` (the index
    of the current bar,
    [bar_index](https://www.tradingview.com/pine-script-reference/v5/#var_bar_index))
    and `y = high` (the bar\'s
    [high](https://www.tradingview.com/pine-script-reference/v5/#var_high)
    value).
-   We do not supply an argument for the function\'s `text` parameter.
    Its default value being an empty string, no text is displayed.
-   No logic controls our
    [label.new()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
    call, so labels are created on every bar.
-   Only the last 54 labels are displayed because our
    [indicator()](https://www.tradingview.com/pine-script-reference/v5/#fun_indicator)
    call does not use the `max_labels_count` parameter to specify a
    value other than the \~50 default.
-   Labels persist on bars until your script deletes them using
    [label.delete()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete),
    or garbage collection removes them.

In the next example we display a label on the bar with the highest
[high](https://www.tradingview.com/pine-script-reference/v5/#var_high)
value in the last 50 bars:

``` pine
//@version=5
indicator("", "", true)

// Find the highest `high` in last 50 bars and its offset. Change it's sign so it is positive.
LOOKBACK = 50
hi = ta.highest(LOOKBACK)
highestBarOffset = - ta.highestbars(LOOKBACK)

// Create label on bar zero only.
var lbl = label.new(na, na, "", color = color.orange, style = label.style_label_lower_left)
// When a new high is found, move the label there and update its text and tooltip.
if ta.change(hi)
    // Build label and tooltip strings.
    labelText = "High: " + str.tostring(hi, format.mintick)
    tooltipText = "Offest in bars: " + str.tostring(highestBarOffset) + "\nLow: " + str.tostring(low[highestBarOffset], format.mintick)
    // Update the label's position, text and tooltip.
    label.set_xy(lbl, bar_index[highestBarOffset], hi)
    label.set_text(lbl, labelText)
    label.set_tooltip(lbl, tooltipText)
```

![image](@assets/concepts/TextAndShapes-CreatingLabels-02.png)

Note that:

-   We create the label on the first bar only by using the
    [var](https://www.tradingview.com/pine-script-reference/v5/#kw_var)
    keyword to declare the `lbl` variable that contains the label\'s ID.
    The `x`, `y` and `text` arguments in that
    [label.new()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
    call are irrelevant, as the label will be updated on further bars.
    We do, however, take care to use the `color` and `style` we want for
    the labels, so they don\'t need updating later.
-   On every bar, we detect if a new high was found by testing for
    changes in the value of `hi`
-   When a change in the high value occurs, we update our label with new
    information. To do this, we use three `label.set*()` calls to change
    the label\'s relevant information. We refer to our label using the
    `lbl` variable, which contains our label\'s ID. The script is thus
    maintaining the same label throughout all bars, but moving it and
    updating its information when a new high is detected.

Here we create a label on each bar, but we set its properties
conditionally, depending on the bar\'s polarity:

``` pine
//@version=5
indicator("", "", true)
lbl = label.new(bar_index, na)
if close >= open
    label.set_text( lbl, "green")
    label.set_color(lbl, color.green)
    label.set_yloc( lbl, yloc.belowbar)
    label.set_style(lbl, label.style_label_up)
else
    label.set_text( lbl, "red")
    label.set_color(lbl, color.red)
    label.set_yloc( lbl, yloc.abovebar)
    label.set_style(lbl, label.style_label_down)
```

![image](@assets/concepts/TextAndShapes-CreatingLabels-03.png)

### Positioning labels

Labels are positioned on the chart according to *x* (bars) and *y*
(price) coordinates. Five parameters affect this behavior: `x`, `y`,
`xloc`, `yloc` and `style`:

`x`

    Is either a bar index or a time value. When a bar index is used, the
    value can be offset in the past or in the future (maximum of 500
    bars in the future). Past or future offsets can also be calculated
    when using time values. The `x` value of an existing label can be
    modified using
    [label.set_x()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_x)
    or
    [label.set_xy()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy).

`xloc`

    Is either
    [xloc.bar_index](https://www.tradingview.com/pine-script-reference/v5/#const_xloc%7Bdot%7Dbar_index)
    (the default) or
    [xloc.bar_time](https://www.tradingview.com/pine-script-reference/v5/#const_xloc%7Bdot%7Dbar_time).
    It determines which type of argument must be used with `x`. With
    [xloc.bar_index](https://www.tradingview.com/pine-script-reference/v5/#const_xloc%7Bdot%7Dbar_index),
    `x` must be an absolute bar index. With
    [xloc.bar_time](https://www.tradingview.com/pine-script-reference/v5/#const_xloc%7Bdot%7Dbar_time),
    `x` must be a UNIX time in milliseconds corresponding to the
    [time](https://www.tradingview.com/pine-script-reference/v5/#var_time)
    value of a bar\'s
    [open](https://www.tradingview.com/pine-script-reference/v5/#var_open).
    The `xloc` value of an existing label can be modified using
    [label.set_xloc()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xloc).

`y`

    Is the price level where the label is positioned. It is only taken
    into account with the default `yloc` value of `yloc.price`. If
    `yloc` is
    [yloc.abovebar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dabovebar)
    or
    [yloc.belowbar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dbelowbar)
    then the `y` argument is ignored. The `y` value of an existing label
    can be modified using
    [label.set_y()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_y)
    or
    [label.set_xy()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy).

`yloc`

    Can be
    [yloc.price](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dprice)
    (the default),
    [yloc.abovebar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dabovebar)
    or
    [yloc.belowbar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dbelowbar).
    The argument used for `y` is only taken into account with
    [yloc.price](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dprice).
    The `yloc` value of an existing label can be modified using
    [label.set_yloc()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_yloc).

`style`

    The argument used has an impact on the visual appearance of the
    label and on its position relative to the reference point determined
    by either the `y` value or the top/bottom of the bar when
    [yloc.abovebar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dabovebar)
    or
    [yloc.belowbar](https://www.tradingview.com/pine-script-reference/v5/#const_yloc%7Bdot%7Dbelowbar)
    are used. The `style` of an existing label can be modified using
    [label.set_style()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_style).

These are the available `style` arguments:

  -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
  Argument                     Label                                                                            Label with text                                                                         Argument                          Label                                                                                    Label with text
  ---------------------------- -------------------------------------------------------------------------------- ------------------------------------------------------------------------------------ -- --------------------------------- ---------------------------------------------------------------------------------------- --------------------------------------------------------------------------------------------
  `label.style_xcross`         ![label_style_xcross](@assets/concepts/TextAndShapes-LabelStyles-xcross.png)               ![label_style_xcross_t](@assets/concepts/TextAndShapes-LabelStyles-xcross_t.png)                  `label.style_label_up`            ![label_style_label_up](@assets/concepts/TextAndShapes-LabelStyles-labelup.png)                    ![label_style_label_up_t](@assets/concepts/TextAndShapes-LabelStyles-labelup_t.png)

  `label.style_cross`          ![label_style_cross](@assets/concepts/TextAndShapes-LabelStyles-cross.png)                 ![label_style_cross_t](@assets/concepts/TextAndShapes-LabelStyles-cross_t.png)                    `label.style_label_down`          ![label_style_label_down](@assets/concepts/TextAndShapes-LabelStyles-labeldown.png)                ![label_style_label_down_t](@assets/concepts/TextAndShapes-LabelStyles-labeldown_t.png)

  `label.style_flag`           ![label_style_flag](@assets/concepts/TextAndShapes-LabelStyles-flag.png)                   ![label_style_flag_t](@assets/concepts/TextAndShapes-LabelStyles-flag_t.png)                      `label.style_label_left`          ![label_style_label_left](@assets/concepts/TextAndShapes-LabelStyles-labelleft.png)                ![label_style_label_left_t](@assets/concepts/TextAndShapes-LabelStyles-labelleft_t.png)

  `label.style_circle`         ![label_style_circle](@assets/concepts/TextAndShapes-LabelStyles-circle.png)               ![label_style_circle_t](@assets/concepts/TextAndShapes-LabelStyles-circle_t.png)                  `label.style_label_right`         ![label_style_label_right](@assets/concepts/TextAndShapes-LabelStyles-labelright.png)              ![label_style_label_right_t](@assets/concepts/TextAndShapes-LabelStyles-labelright_t.png)

  `label.style_square`         ![label_style_square](@assets/concepts/TextAndShapes-LabelStyles-square.png)               ![label_style_square_t](@assets/concepts/TextAndShapes-LabelStyles-square_t.png)                  `label.style_label_lower_left`    ![label_style_label_lower_left](@assets/concepts/TextAndShapes-LabelStyles-labellowerleft.png)     ![label_style_label_lower_left_t](@assets/concepts/TextAndShapes-LabelStyles-labellowerleft_t.png)

  `label.style_diamond`        ![label_style_diamond](@assets/concepts/TextAndShapes-LabelStyles-diamond.png)             ![label_style_diamond_t](@assets/concepts/TextAndShapes-LabelStyles-diamond_t.png)                `label.style_label_lower_right`   ![label_style_label_lower_right](@assets/concepts/TextAndShapes-LabelStyles-labellowerright.png)   ![label_style_label_lower_right_t](@assets/concepts/TextAndShapes-LabelStyles-labellowerright_t.png)

  `label.style_triangleup`     ![label_style_triangleup](@assets/concepts/TextAndShapes-LabelStyles-triangleup.png)       ![label_style_triangleup_t](@assets/concepts/TextAndShapes-LabelStyles-triangleup_t.png)          `label.style_label_upper_left`    ![label_style_label_upper_left](@assets/concepts/TextAndShapes-LabelStyles-labelupperleft.png)     ![label_style_label_upper_left_t](@assets/concepts/TextAndShapes-LabelStyles-labelupperleft_t.png)

  `label.style_triangledown`   ![label_style_triangledown](@assets/concepts/TextAndShapes-LabelStyles-triangledown.png)   ![label_style_triangledown_t](@assets/concepts/TextAndShapes-LabelStyles-triangledown_t.png)      `label.style_label_upper_right`   ![label_style_label_upper_right](@assets/concepts/TextAndShapes-LabelStyles-labelupperright.png)   ![label_style_label_upper_right_t](@assets/concepts/TextAndShapes-LabelStyles-labelupperright_t.png)

  `label.style_arrowup`        ![label_style_arrowup](@assets/concepts/TextAndShapes-LabelStyles-arrowup.png)             ![label_style_arrowup_t](@assets/concepts/TextAndShapes-LabelStyles-arrowup_t.png)                `label.style_label_center`        ![label_style_label_center](@assets/concepts/TextAndShapes-LabelStyles-labelcenter.png)            ![label_style_label_center_t](@assets/concepts/TextAndShapes-LabelStyles-labelcenter_t.png)

  `label.style_arrowdown`      ![label_style_arrowdown](@assets/concepts/TextAndShapes-LabelStyles-arrowdown.png)         ![label_style_arrowdown_t](@assets/concepts/TextAndShapes-LabelStyles-arrowdown_t.png)            `label.style_none`                                                                                                         ![label_style_none_t](@assets/concepts/TextAndShapes-LabelStyles-none_t.png)
  -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

When using
[xloc.bar_time](https://www.tradingview.com/pine-script-reference/v5/#const_xloc%7Bdot%7Dbar_time),
the `x` value must be a UNIX timestamp in milliseconds. See the page on
`Time <PageTime>` for more information.
The start time of the current bar can be obtained from the
[time](https://www.tradingview.com/pine-script-reference/v5/#var_time)
built-in variable. The bar time of previous bars is `time[1]`, `time[2]`
and so on. Time can also be set to an absolute value with the
[timestamp](https://www.tradingview.com/pine-script-reference/v5/#fun_timestamp)
function. You may add or subtract periods of time to achieve relative
time offset.

Let\'s position a label one day ago from the date on the last bar:

``` pine
//@version=5
indicator("")
daysAgoInput = input.int(1, tooltip = "Use negative values to offset in the future")
if barstate.islast
    MS_IN_ONE_DAY = 24 * 60 * 60 * 1000
    oneDayAgo = time - (daysAgoInput * MS_IN_ONE_DAY)
    label.new(oneDayAgo, high, xloc = xloc.bar_time, style = label.style_label_right)
```

Note that because of varying time gaps and missing bars when markets are
closed, the positioning of the label may not always be exact. Time
offsets of the sort tend to be more reliable on 24x7 markets.

You can also offset using a bar index for the `x` value, e.g.:

    label.new(bar_index + 10, high)
    label.new(bar_index - 10, high[10])
    label.new(bar_index[10], high[10])

### Reading label properties

The following *getter* functions are available for labels:

-   [label.get_x()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dget_x)
-   [label.get_y()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dget_y)
-   [label.get_text()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dget_text)

They all have a similar signature. The one for
[label.get_text()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dget_text)
is:

``` text
label.get_text(id) → series string
```

where `id` is the label whose text is to be retrieved.

### Cloning labels

The
[label.copy()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dcopy)
function is used to clone labels. Its syntax is:

``` text
label.copy(id) → void
```

### Deleting labels

The
[label.delete()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete)
function is used to delete labels. Its syntax is:

``` text
label.delete(id) → void
```

To keep only a user-defined quantity of labels on the chart, one could
use code like this:

``` pine
//@version=5
MAX_LABELS = 500
indicator("", max_labels_count = MAX_LABELS)
qtyLabelsInput = input.int(5, "Labels to keep", minval = 0, maxval = MAX_LABELS)
myRSI = ta.rsi(close, 20)
if myRSI > ta.highest(myRSI, 20)[1]
    label.new(bar_index, myRSI, str.tostring(myRSI, "#.00"), style = label.style_none)
    if array.size(label.all) > qtyLabelsInput
        label.delete(array.get(label.all, 0))
plot(myRSI)
```

![image](@assets/concepts/TextAndShapes-DeletingLabels-01.png)

Note that:

-   We define a `MAX_LABELS` constant to hold the maximum quantity of
    labels a script can accommodate. We use that value to set the
    `max_labels_count` parameter\'s value in our
    [indicator()](https://www.tradingview.com/pine-script-reference/v5/#fun_indicator)
    call, and also as the `maxval` value in our
    [input.int()](https://www.tradingview.com/pine-script-reference/v5/#fun_input%7Bdot%7Dint)
    call to cap the user value.
-   We create a new label when our RSI breaches its highest value of the
    last 20 bars. Note the offset of `[1]` we use in
    `if myRSI > ta.highest(myRSI, 20)[1]`. This is necessary. Without
    it, the value returned by
    [ta.highest()](https://www.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dhighest)
    would always include the current value of `myRSI`, so `myRSI` would
    never be higher than the function\'s return value.
-   After that, we delete the oldest label in the
    [label.all](https://www.tradingview.com/pine-script-reference/v5/#var_label%7Bdot%7Dall)
    array that is automatically maintained by the Pine Script™ runtime
    and contains the ID of all the visible labels drawn by our script.
    We use the
    [array.get()](https://www.tradingview.com/pine-script-reference/v5/#fun_array%7Bdot%7Dget)
    function to retrieve the array element at index zero (the oldest
    visible label ID). We then use
    [label.delete()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete)
    to delete the label linked with that ID.

Note that if one wants to position a label on the last bar only, it is
unnecessary and inefficent to create and delete the label as the script
executes on all bars, so that only the last label remains:

``` pine
// INEFFICENT!
//@version=5
indicator("", "", true)
lbl = label.new(bar_index, high, str.tostring(high, format.mintick))
label.delete(lbl[1])
```

This is the efficient way to realize the same task:

``` pine
//@version=5
indicator("", "", true)
if barstate.islast
    // Create the label once, the first time the block executes on the last bar.
    var lbl = label.new(na, na)
    // On all iterations of the script on the last bar, update the label's information.
    label.set_xy(lbl, bar_index, high)
    label.set_text(lbl, str.tostring(high, format.mintick))
```

### Realtime behavior

Labels are subject to both *commit* and *rollback* actions, which affect
the behavior of a script when it executes in the realtime bar. See the
page on Pine Script™\'s
`Execution model <Page_ExecutionModel>`.

This script demonstrates the effect of rollback when running in the
realtime bar:

``` pine
//@version=5
indicator("", "", true)
label.new(bar_index, high)
```

On realtime bars,
[label.new()](https://www.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
creates a new label on every script update, but because of the rollback
process, the label created on the previous update on the same bar is
deleted. Only the last label created before the realtime bar\'s close
will be committed, and thus persist.


